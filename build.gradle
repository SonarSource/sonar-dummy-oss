plugins {
  id 'com.github.hierynomus.license' version '0.16.1'
  id "com.jfrog.artifactory" version "4.26.1"
  id "org.sonarqube" version "3.4.0.2513"
  id 'signing'
  // id "org.cyclonedx.bom" version "1.5.0"
}

// Analyze with SonarQube!
sonarqube {
  properties {
    property 'sonar.projectName', 'SonarSource :: dummy'
    property 'sonar.projectKey', 'org.sonarsource.dummy:dummy-oss'
  }
}

allprojects {
  apply plugin: 'com.jfrog.artifactory'
  apply plugin: 'maven-publish'
  // apply plugin: "org.cyclonedx.bom"

  // Replaces the version defined in sources, usually x.y-SNAPSHOT, by a version identifying the build.
  def buildNumber = System.getProperty("buildNumber")
  if (version.endsWith('-SNAPSHOT') && buildNumber != null) {
    version = version.replace('-SNAPSHOT', ".0.$buildNumber")
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url System.env.'ARTIFACTORY_URL' + '/sonarsource'
    }
  }

  signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    required {
      def branch = System.getenv()["CIRRUS_BRANCH"]
      return (branch == 'master' || branch ==~ 'branch-[\\d.]+') &&
        gradle.taskGraph.hasTask(":artifactoryPublish")
    }
    sign publishing.publications
  }

  tasks.withType(Sign) {
    onlyIf {
      def branch = System.getenv()["CIRRUS_BRANCH"]
      return !artifactoryPublish.skip &&
        (branch == 'master' || branch ==~ 'branch-[\\d.]+') &&
        gradle.taskGraph.hasTask(":artifactoryPublish")
    }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: "jacoco"
  apply plugin: 'com.github.hierynomus.license'

  java {
    withJavadocJar()
    withSourcesJar()
  }

  compileJava {
    options.release = 8
    options.encoding = 'UTF-8'
  }

  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  license {
    header = rootProject.file('HEADER')
    strictCheck true
    mapping {
      java = 'SLASHSTAR_STYLE'
    }
    includes(['**/*.java'])
  }

  // def bomFile = layout.buildDirectory.file('reports/bom.json')
  // cyclonedxBom {
  //   outputs.file bomFile
  // }
  // def bomArtifact = artifacts.add('archives', bomFile.get().asFile) {
  //   type 'json'
  //   classifier 'cyclonedx'
  //   builtBy 'cyclonedxBom'
  // }
  // publishing {
  //   publications {
  //     mavenJava(MavenPublication) {
  //       artifact bomArtifact
  //     }
  //   }
  // }
}

artifactory {
  clientConfig.setIncludeEnvVars(true)
  clientConfig.setEnvVarsExcludePatterns('*password*,*PASSWORD*,*secret*,*MAVEN_CMD_LINE_ARGS*,sun.java.command,*token*,*TOKEN*,*LOGIN*,*login*,*signing*')
  contextUrl = System.getenv('ARTIFACTORY_URL')
  publish {
    repository {
      repoKey = System.getenv('ARTIFACTORY_DEPLOY_REPO')
      username = System.getenv('ARTIFACTORY_DEPLOY_USERNAME')
      password = System.getenv('ARTIFACTORY_DEPLOY_PASSWORD')
    }
    defaults {
      properties = [
        'build.name'      : 'sonar-dummy-oss',
        'build.number'    : System.getenv('BUILD_NUMBER'),
        'pr.branch.target': System.getenv('PULL_REQUEST_BRANCH_TARGET'),
        'pr.number'       : System.getenv('PULL_REQUEST_NUMBER'),
        'vcs.branch'      : System.getenv('GIT_BRANCH'),
        'vcs.revision'    : System.getenv('GIT_COMMIT'),
        'version'         : version
      ]
      publications('mavenJava')
      publishPom = true
      publishIvy = false
    }
    clientConfig.info.addEnvironmentProperty('ARTIFACTS_TO_PUBLISH', 'org.sonarsource.dummy:sonar-dummy-oss-plugin:jar')
  }

  clientConfig.info.setBuildName('sonar-dummy-oss')
  clientConfig.info.setBuildNumber(System.getenv('BUILD_NUMBER'))
  // The name of this variable is important because it's used by the delivery process when extracting version from Artifactory build info.
  clientConfig.info.addEnvironmentProperty('PROJECT_VERSION', "${version}")
}
